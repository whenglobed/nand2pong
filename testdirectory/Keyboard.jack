// This file is part of the materials accompanying the book 
// "The Elements of Computing Systems" by Nisan and Schocken, 
// MIT Press. Book site: www.idc.ac.il/tecs
// File name: projects/12/Keyboard.jack

/**
 * A library for handling user input from the keyboard.
 */
class Keyboard {

    static int keyboardMemoryMap;    
    
    /** Initializes the keyboard. */
    function void init() {
        let keyboardMemoryMap = 24576;
        return;
    } 

    /**
     * Returns the ASCII code (as char) of the currently pressed key,
     * or 0 if no key is currently pressed.
     * Recognizes all ASCII characters, and the following extension
     * of action keys:
     * New line = 128 = String.newline()
     * Backspace = 129 = String.backspace()
     * Left Arrow = 130
     * Up Arrow = 131
     * Right Arrow = 132
     * Down Arrow = 133
     * Home = 134
     * End = 135
     * Page Up = 136
     * Page Down = 137
     * Insert = 138
     * Delete = 139
     * ESC = 140
     * F1 - F12 = 141 - 152
     */
    function char keyPressed() {
        return Memory.peek(keyboardMemoryMap);
    }

    /**								
     * Reads the next character from the keyboard.
     * waits until a key is pressed and then released, then echoes
     * the key to the screen, and returns the value of the pressed key.
     */
    function char readChar() {
        var char c;
        
        let c = 0;
        
        while (c = 0) {
            let c = Keyboard.keyPressed();
        }
        
        while (~(Keyboard.keyPressed() = 0)) {
            // wait for user to let go
        }
        
        do Output.printChar(c);
        return c;
    }

    /**								
     * prints the message on the screen and then reads the next line
     * (until a newline character) from the keyboard and returns its value.
     */
    function String readLine(String message) {        
        var char c;
        var String s;        
        
        let c = 0;
        let s = String.new(64); // enough for one whole line        
        
        do Output.printString(message);
        
        while (~(c = 128)) {
            let c = Keyboard.readChar();
            if (c = 129) {                
                if (s.length() > 0) {
                    do s.eraseLastChar();
                    do Output.backSpace();
                }
            }
            else {
                if (~(c = 128)) {
                    do s.appendChar(c);
                }
            }
        }
        return s;
    }

    /**								
     * prints the message on the screen and then reads the next line
     * (until a newline character) from the keyboard and returns its
     * integer value (until the first non numeric character).
     */
    function int readInt(String message) {
        var String s;
        
        let s = Keyboard.readLine(message);
        return s.intValue();
    }
}
