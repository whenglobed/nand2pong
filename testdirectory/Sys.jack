// This file is part of the materials accompanying the book 
// "The Elements of Computing Systems" by Nisan and Schocken, 
// MIT Press. Book site: www.idc.ac.il/tecs
// File name: projects/12/Sys.jack

/**
 * A basic system services library.
 */
class Sys {

    /** Makes all required initializations. */
    function void init() {
        do Memory.init();
        do Screen.init();
        do Keyboard.init();
        do Output.init();
        do Math.init();
        
        do Main.main();
        do Sys.halt();
        return;
    }

    /** Halts the execution. */
    function void halt() {
        while (true) {
            // infinite loop
        }
        return;
    }

    /** Waits approximately duration milliseconds before it returns. */
    function void wait(int duration) {
        var int i, timer;
        let i = 0;
        let timer = 0;
        while (i < duration) {
            while (timer < 325) {
                let timer = timer + 1;
            }
            let i = i + 1;
            let timer = 0;
        }
        return;
    }

    /** prints the given error code in the form "ERR<errorCode>",
     *  and halts. */
    function void error(int errorCode) {
        do Output.printString("ERR");
        do Output.printInt(errorCode);
        do Sys.halt();
        return;
    }
}
